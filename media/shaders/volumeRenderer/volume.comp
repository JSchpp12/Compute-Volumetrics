#version 450

const float PI = 3.1415926535897932384626433832795;

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

struct Light_Type
{
    uint point;
    uint directional;
    uint spot;
};

struct Light
{
    vec4 position;
    vec4 direction;

    // properties
    vec4 ambient;
    vec4 diffuse;
    vec4 specular;
    // controls.x = inner cutoff angle
    // controls.y = outer cutoff angle
    vec4 controls;
    // settings.x = enabled
    // settings.y = type
    uvec4 settings;
};

layout(set = 0, binding = 0) uniform GlobalUniformBufferObject
{
    mat4 proj;
    mat4 view;
    mat4 invView;
} globalUbo;

layout(set = 0, binding = 1) uniform GlobalLightInfo{
    int numLights;
} lightInfo; 

layout(set = 0, binding = 2) readonly buffer GlobalLightBuffer{
    Light lights[];
 } lightList;

layout(set = 1, binding = 0) uniform additionalCameraInfo
{
    mat4 invProj;
    vec2 cameraDimensions;
    float aspectRatio;
    float farClipDist;
    float nearClipDist;
    float scale;
}
addCamInfo;
layout(std430, set = 1, binding = 1) buffer PNanoVDBBuffer{
    uint pnanovdb_buf_data[];
};
layout(r32f, set = 1, binding = 2) uniform readonly image2D randomTexture;

layout(set = 2, binding = 0, rgba8) uniform readonly image2D inputSceneColor;
layout(set = 2, binding = 1) uniform sampler2D inputSceneDepth;
layout(set = 2, binding = 2, rgba8) uniform writeonly image2D outputImage;
layout(set = 2, binding = 3) buffer RayDistanceResult{
    float rayDistData[]; 
};

layout(set = 3, binding = 0) uniform InstanceModelMatrix
{
    mat4 instanceModelMatrix[1024];
};

layout(set = 3, binding = 1) uniform aabbInfo
{
    vec4 bounds[2];
}
aabb;

layout(set = 3, binding = 2) uniform FogControllerInfo
{
    float linearFog_nearDistance;
    float linearFog_farDistance;
    float expFog_density;
    float marchedFog_defaultDensity;
    float marchedFog_sigmaAbsorption;
    float marchedFog_sigmaScattering;
    float marchedFog_lightPropertyDirG;
    float marchedFog_stepSizeDist;
    float marchedFog_stepSizeDist_light;
    uint marchedFog_levelSet_maxNumSteps;
    uint gridType;
}
fogInfo;

layout(set = 3, binding = 3) uniform InstanceInverseModelMatrix{
    mat4 instanceInverseMatrix[1024];
};

#include "PNanoVDB.h"
#include "ray_helpers.comp"
#include "volume_helpers.comp"


ivec2 getTargetPixelCoords()
{
    return ivec2((gl_LocalInvocationID.x) + ((gl_WorkGroupID.x) * 8),
                 (gl_LocalInvocationID.y) + ((gl_WorkGroupID.y) * 8));
}

float linearizeDepth(const float depth, const float camNearClipDist, const float camFarClipDist)
{
    /* convert depth value back to linear space -- based on proj matrix
    Source: http://www.songho.ca/opengl/gl_projectionmatrix.html
    Source2: https://learnopengl.com/Advanced-OpenGL/Depth-testing
    */
    return (camNearClipDist * camFarClipDist) / (camFarClipDist - depth * (camFarClipDist - camNearClipDist));
}

bool checkForThreadOutsideOfTarget()
{
    ivec2 imageSize = imageSize(inputSceneColor);
    uvec2 coords = gl_GlobalInvocationID.xy;
    if (coords.x > imageSize.x || coords.y > imageSize.y)
    {
        return false;
    }

    return true;
}

float applyHenyeyGreensteinPhase(const float cosTheta, const float g){
    float denom = 1.0 + g*g - 2.0*g*cosTheta;
    return (1.0 / (4.0 * PI)) * (1.0 - g*g) / pow(denom, 1.5);
}

vec3 forwardMarch(in pnanovdb_grid_handle_t grid, const vec3 backgroundColor, const Ray ray, const float t0, const float t1,
                  const mat4 inverseModelMatrix, const float volumeUniformScale, float sigmaAbsorption, float sigmaScattering, float lightPropertyDir_g,
                  float mainStepSize, float lightStepSize, const float cutOffDistance, const float jitterAmount, const float defaultDensity)
{ 
    pnanovdb_tree_handle_t tree = pnanovdb_grid_get_tree(nanoVDBBuffer, grid);
    pnanovdb_root_handle_t root = pnanovdb_tree_get_root(nanoVDBBuffer, tree);
    pnanovdb_readaccessor_t acc;
    pnanovdb_readaccessor_init(acc, root);
    pnanovdb_grid_type_t type = pnanovdb_grid_get_grid_type(nanoVDBBuffer, grid); 

    // const float endDist = min(t1, cutOffDistance); 
    const int fittedNumSteps = max(1, int(ceil(t1 / mainStepSize)));
    const float middleStepSizeDist = mainStepSize * 0.5; 
    const float sigmaT = sigmaAbsorption + sigmaScattering;
    const float jitter = jitterAmount * mainStepSize; 
    const float lJitter = jitterAmount * lightStepSize; 

	float transmission = 1.0;
    vec3 resultingRadiance = vec3(0.0);
    for (int i = 0; i < fittedNumSteps; i++){
        // setting to middle
        const float t = (t0 + (mainStepSize * i)) + middleStepSizeDist + jitter;
        if (t > cutOffDistance)
            break; 

		const vec3 pos = ray.origin.xyz + (t * ray.direction.xyz); 
        const float densityP = gridReadAt(grid, acc, type, pos);
        const float pTransmission = exp(-densityP * sigmaT * mainStepSize);

		for (int j = 0; j < lightInfo.numLights; j++){
            //might need to check if light is within volume later
            const vec3 lightPos = (inverseModelMatrix * lightList.lights[j].position).xyz; 
            const Ray rayToLight = createRay(pos, normalize(lightPos - pos));

            float lt0 = 0;
            float lt1 = 0;  
            if (rayBoxIntersect(grid, rayToLight, lt0, lt1)){ 
                float lTau = 0.0;

                const int fittedNumStepsLight = max(1, int(ceil(lt1 / lightStepSize)));
                const float middleStepSizeDistLight = lightStepSize * 0.5;  

                for (int k = 0; k < fittedNumStepsLight; k++){
                    const float lt = lt0 + (lightStepSize * k) + middleStepSizeDistLight + lJitter;
                    const vec3 lPos = pos + (lt * rayToLight.direction.xyz); 
                    const float lDensity = gridReadAt(grid, acc, type, lPos); 
                    lTau += lDensity * sigmaT * lightStepSize;
                } 

                const float lRayAttenuation = exp(-lTau);
                const float cosTheta = dot(-ray.direction.xyz, rayToLight.direction.xyz);
                const float phase = applyHenyeyGreensteinPhase(cosTheta, lightPropertyDir_g); 
                resultingRadiance += transmission * lightList.lights[j].ambient.rgb * lRayAttenuation * phase * sigmaScattering * densityP * mainStepSize; 
                transmission *= pTransmission;
            }
		} 
    }

    return backgroundColor * transmission + resultingRadiance; 
}

void main()
{
    uvec2 gid = gl_GlobalInvocationID.xy;
    uvec2 size = uvec2(addCamInfo.cameraDimensions.xy);
    if (any(greaterThanEqual(gid, size))) return;

    ivec2 targetPixel = getTargetPixelCoords(); 
    pnanovdb_grid_handle_t grid = makeNanoGrid(0);
    const float volumeUniformScale = extractUniformVolumeScale(instanceModelMatrix[0]);
    const float randomValue = imageLoad(randomTexture, targetPixel).r;
    
    float targetPixelInputDepth = texelFetch(inputSceneDepth, targetPixel, 0).r;
    const float linearDepth = linearizeDepth(targetPixelInputDepth, addCamInfo.nearClipDist, addCamInfo.farClipDist); 
    vec3 resultingColor = imageLoad(inputSceneColor, targetPixel).rgb;

    vec3 viewDirScene = vec3(0.0); 
    vec3 orgScene = vec3(0.0);
    getSceneViewInfoForPix(targetPixel, uvec2(addCamInfo.cameraDimensions.x, addCamInfo.cameraDimensions.y), targetPixelInputDepth, orgScene, viewDirScene); 

    const mat4 inverseModelMatrix = inverse(instanceModelMatrix[0]); 
    Ray worldRay = createRay(orgScene.xyz, viewDirScene.xyz);  
    Ray localRay = transformWorldRayToGridLocal(worldRay, inverseModelMatrix); 

    float t0 = 0;
    float t1 = 0;
    if (rayBoxIntersect(grid, localRay, t0, t1) && t0 < linearDepth){
        resultingColor = forwardMarch(grid,
            resultingColor, 
            localRay, 
            t0, 
            t1, 
            inverseModelMatrix, 
            volumeUniformScale,
            fogInfo.marchedFog_sigmaAbsorption,
            fogInfo.marchedFog_sigmaScattering,
            fogInfo.marchedFog_lightPropertyDirG, 
            fogInfo.marchedFog_stepSizeDist,
            fogInfo.marchedFog_stepSizeDist_light,
            linearDepth,
            randomValue,
            fogInfo.marchedFog_defaultDensity
        );
    }

    imageStore(outputImage, targetPixel, vec4(resultingColor, 1.0));
}